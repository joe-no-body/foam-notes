# Sunday, April 3, 2022

## Displaying running commands in the terminal title with zsh

#zsh

Often, I'll have a terminal window open with a bunch of tabs for different man
pages or other things, which can start to get confusing to navigate after a
while.

I previously set up zsh to update the terminal title bar with the pwd based on
3.6 from https://zsh.sourceforge.io/FAQ/zshfaq03.html, but today I decided I
also want to see the name of the running command. 

This turned out to be pretty easy to implement using zsh's `preexec` hook.

```zsh
set_term_title() {
  __term_title="$*"
  case $TERM in
    sun-cmd) print -n "\e]l$__term_title\e\\"
      ;;
    *xterm*|rxvt|(dt|k|E)term) print -n "\e]2;$__term_title\a"
      ;;
  esac
}

# via 3.6 here: https://zsh.sourceforge.io/FAQ/zshfaq03.html
set_term_title_to_pwd() {
  [[ -t 1 ]] || return
  set_term_title "$(print -P %~)"
}

# Set the terminal title right away.
set_term_title_to_pwd

set_term_title_to_cmd() {
  set_term_title "$1"
}

# Update the terminal title on cd.
chpwd_functions+=(set_term_title_to_pwd)

# Change the title to the running command so long-running commands like man are
# easier to identify.
preexec_functions+=(set_term_title_to_cmd)

# Set the title back to pwd when the prompt is displayed. This resets the title
# after a long-running command terminates.
precmd_functions+=(set_term_title_to_pwd)
```
